#!/usr/bin/env zsh
# take a terminal screenshot with `freeze`
#
# usage:
#   $0       OUTFILE
#   $0 --txt OUTFILE
#
# --txt:
#   don't run `freeze`, instead capture stdout as plaintext
#
# OUTFILE:
#   file to write
#   must match /.*(tidy|checks).*/

emulate zsh
set -ue -o pipefail

# constants:
heightPxPerLine=27
width=690
runTest=./scripts/montage/runTest

# parse arguments:
integer txt=0 ; [[ $1 == --txt ]] && txt=1 && shift
OUTFILE=$1


# implementation
# --------------

outdir=${OUTFILE:h}
height=$((
  nLines = 1 + $(${runTest} checks | wc -l),
  heightPxPerLine * nLines
  ))
formatter=$(<<< $OUTFILE grep -oE 'tidy|checks')
invokeCmd=${runTest}\ ${formatter}
freezeArgs=(
  --width     ${width}
  --height    ${height}
  --font.size 22
  --execute   ${invokeCmd}
  --theme     github-dark
  --output    ${OUTFILE}
  )

mkdir -p ${outdir}
if ! ((txt))
then freeze ${freezeArgs}
else eval ${invokeCmd} >${OUTFILE}
fi
